plugins {
    id "java-library"
    id "maven-publish"
    id "net.kyori.blossom" version "2.0.1"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    group = project.maven_group ?: rootProject.maven_group
    archivesName = project.maven_name ?: rootProject.maven_name
    version = project.maven_version ?: rootProject.maven_version
}

repositories {
    mavenCentral()
    maven {
        name = "ViaVersion"
        url "https://repo.viaversion.com"
    }
}

dependencies {
    implementation "com.viaversion:viaversion:4.8.0-23w31a-SNAPSHOT"
    implementation "org.yaml:snakeyaml:2.2"
    implementation "com.google.guava:guava:32.1.2-jre"
    implementation "io.netty:netty-handler:4.1.97.Final"

    api "net.lenni0451.mcstructs:text:2.3.1"
}

blossom {
    replaceToken("\${version}", project.version, "src/main/java/net/raphimc/vialegacy/ViaLegacy.java")
    replaceToken("\${impl_version}", "git-${project.name}-${project.version}:${latestCommitHash()}", "src/main/java/net/raphimc/vialegacy/ViaLegacy.java")
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.name ?: rootProject.name}" }
    }
}

publishing {
    repositories {
        maven {
            name = "Via"
            url = "https://repo.viaversion.com/"

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java

            pom {
                name = "ViaLegacy"
                description = "ViaVersion addon to add support for EVERY Minecraft server version (Classic, Alpha, Beta, Release)"
                url = "https://github.com/ViaVersion/ViaLegacy"
                licenses {
                    license {
                        name = "GPL-3.0 License"
                        url = "https://github.com/ViaVersion/ViaLegacy/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "RK_01"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/ViaVersion/ViaLegacy.git"
                    developerConnection = "scm:git:ssh://github.com/ViaVersion/ViaLegacy.git"
                    url = "https://github.com/ViaVersion/ViaLegacy.git"
                }
            }
        }
    }
}

String latestCommitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
